#! /usr/bin/awk -f

# dhms - convert time durations between seconds and days/hours/minutes/seconds.
# Copyright (C) 2022 Erik Auerswald <auerswal@unix-ag.uni-kl.de>
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.

# This is a simple Awk based Unix filter.
# Input is read from files given as command line arguments, or standard input.
# Only non-negative time durations can be converted.
# Each input line is expected to comprise exactly one time duration value.
# A time duration value must either be a unitless number of seconds, or
#  a value in DHMS format (zero valued units can be omitted).
#  Whitespace is ignored.
#  Letters are converted to lower case.
#  47d11h8m15s is an example DHMS value, as is 47min 11s.
# Each input line with a time duration value is replaced with the same
#  time duration value in the other format.

BEGIN {
        progname = "dhms"                  # used as prefix for error messages
}

function dhms_to_secs(dhms) {
        gsub(/[[:space:]]+/, "", dhms)                  # remove whitespace
        val_cnt = split(dhms, vals, "[a-z]+")           # last field is empty
        unit_cnt = split(dhms, units, "[0-9.]+")        # first field is empty
        if (val_cnt != unit_cnt) {
                print progname": Error:" \
                      " number of values differs from number of units:" \
                      " val_cnt=" val_cnt " unit_cnt=" unit_cnt > "/dev/stderr"
                return -1
        }
        secs = 0
        for (i = 1; i < val_cnt; i++) {
                unit = substr(units[i+1], 1, 1)
                if (unit == "d") {
                        secs += vals[i] * 86400
                } else if (unit == "h") {
                        secs += vals[i] * 3600
                } else if (unit == "m") {
                        secs += vals[i] * 60
                } else if (unit == "s") {
                        secs += vals[i]
                } else {
                        print progname": Error: unknown unit "units[i] \
                              > "/dev/stderr"
                        return -1
                }
        }
        return secs
}

function secs_to_dhms(secs) {
        days = int(secs / 86400)
        secs %= 86400
        hours = int(secs / 3600)
        secs %= 3600
        minutes = int(secs / 60)
        secs %= 60
        return days"d"hours"h"minutes"m"secs"s"
}

{
        $0 = tolower($0)      # convert to lower case before checking patterns
}
/^[[:space:]]*([0-9]+\.?[0-9]*|[0-9]*\.?[0-9]+)[[:space:]]*$/ {
        print secs_to_dhms($0)
        next                  # no need to check further patterns
}
/^[[:space:]]*(([0-9]+\.?[0-9]*|[0-9]*\.?[0-9]+)[[:space:]]*(d(ay)?|h(our)?|m(in(ute)?)?|s(ec(ond)?)?)s?[[:space:]]*)+[[:space:]]*$/ {
        secs = dhms_to_secs($0)
        if (secs >= 0) print secs                       # on error, secs == -1
}
